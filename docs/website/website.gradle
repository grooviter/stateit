plugins {
    id 'groovy'
    id "com.bertramlabs.asset-pipeline" version "3.4.4"
    id 'org.ajoberstar.git-publish'
}

dependencies {
    assets('com.github.grooviter:groocss:1.0.0-SNAPSHOT')
    assets "com.bertramlabs.plugins:asset-pipeline-core:$assets_pipeline"
    assets("com.bertramlabs.plugins:groocss-asset-pipeline:$assets_pipeline") {
        exclude module: 'groocss'
    }
}

assets {
    minifyJs         = true
    minifyCss        = false
    enableSourceMaps = false
    maxThreads       = Runtime.runtime.availableProcessors() - 1

    configOptions = [
        commonJs: true,
        enableES6: false,
        groocss: [prettyPrint: true, compress: false, noExts: false, convertUnderline: false]
    ]

    excludes = ['origins/*', '**/jquery/**']
}

gitPublish {
    repoUri       = 'https://github.com/grooviter/stateit.git'
    branch        = 'gh-pages'
    commitMessage = "Released version $version"

    contents {
        from file('build/assets')
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

gitPublishPush {
    dependsOn('setAuthentication','generateSite')
}

task generateSite {
    dependsOn("clean", ":stateit-core:groovydoc", ":guide:asciidoctor", ":website:assetClean", ":website:assetCompile")
    doLast {
        copy {
            from "$rootDir/docs/guide/build/docs/asciidoc"
            into "$rootDir/docs/website/build/assets/guide"
        }
        copy {
            from "$rootDir/modules/stateit-core/build/docs/groovydoc"
            into "$rootDir/docs/website/build/assets/api"
        }
    }
}

task setAuthentication {
    doLast {
        System.setProperty('org.ajoberstar.grgit.auth.username', findProperty('PUBLISH_GH_TOKEN'))
    }
}